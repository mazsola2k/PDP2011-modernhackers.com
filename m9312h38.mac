         .title m9312h38 - standalone high rom - show model and config, then boot rk or rl
;--
;-- Copyright (c) 2008-2019 Sytse van Slooten
;--
;-- Permission is hereby granted to any person obtaining a copy of these VHDL source files and
;-- other language source files and associated documentation files ("the materials") to use
;-- these materials solely for personal, non-commercial purposes.
;-- You are also granted permission to make changes to the materials, on the condition that this
;-- copyright notice is retained unchanged.
;--
;-- The materials are distributed in the hope that they will be useful, but WITHOUT ANY WARRANTY;
;-- without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
;--
;
; general purpose register definitions
;
r0       = %0
r1       = %1
r2       = %2
r3       = %3
r4       = %4
r5       = %5
r6       = %6
r7       = %7
sp       = %6
pc       = %7

         mov #hello+173000,r2
         mov #m000+173000,r5
         jmp barf
m000:
;
; decode cpu modelcode, should be in kernel r0 after reset
;
         mov #47.,r2
m100:
         inc r2
         cmp r0,#10.
         blt m200
         sub #10.,r0
         br m100
m200:
         add #48.,r0
         tstb @#177564
         bpl .-4
         movb r2,@#177566
         tstb @#177564
         bpl .-4
         movb r0,@#177566
;
; probe for fpu
;
         mov #fpuend+173000,r5
         mov #2000,sp
         mov r5,@#10
         clr @#12
         setf
         mov #fpu+173000,r2
         jmp barf
fpuend:
;
; probe for mmu
;
         mov #ktend+173000,r5
         mov #177572,r1
         mov #kt11a+173000,r2
         jmp pbarf
ktend:
;
; probe for mmu ubm
;
         mov #ubend+173000,r5
         mov #170200,r1
         mov #ubma+173000,r2
         jmp pbarf
ubend:
;
; probe for kw controller
;
         mov #kwend+173000,r5
         mov #177564,r1
         mov #kw11a+173000,r2
         jmp pbarf
kwend:
;
; probe for slu controller
;
         mov #slend+173000,r5
         mov #177560,r1
         mov #slu+173000,r2
         jmp pbarf
slend:
;
; probe for rk11 controller
;
;         mov #rkend+173000,r5
;         mov #177400,r1
;         mov #rk11a+173000,r2
;         jmp pbarf
;rkend:
;
; probe for rl11 controller
;
;         mov #rlend+173000,r5
;         mov #174400,r1
;         mov #rl11a+173000,r2
;         jmp pbarf
;rlend:
;
;
;
         mov #hello2+173000,r2
         mov #l1+173000,r5
         jmp barf
l1:
;
; copied (more or less) from simh
;
         mov #brl+173000,r4           ; if probe fails, brl
         mov #brk+173000,r5           ; if probe ok, brk
         mov #177400,r1
         mov #bootrk+173000,r2
         jmp cbarf
;
brk:
         mov #2000,sp
         clr r0
         mov #177412,r1                ; rkda
         clr (r1)                      ; load da = 0
         clr -(r1)                     ; clear ba
         mov #-512.,-(r1)              ; load wc
         mov #5,-(r1)                  ; read+go
         tstb (r1)
         bpl .-2
         clrb (r1)

         clr r0
         clr r2
         clr r3
         mov #2000+20,r4
         clr r5
         clr pc
;
brl:
         mov #nob+173000,r4           ; if probe fails, nob
         mov #brl2+173000,r5          ; if probe ok, brl2
         mov #174400,r1
         mov #bootrl+173000,r2
         jmp cbarf
brl2:
         mov #2000,sp
         clr r0
         mov #174400,r1                ; csr
         mov #13,4(r1)                 ; clear error - set rst in the dar, prepare for get status cmd
         mov #4,(r1)                   ; issue cmd - unit + gstat
         tstb (r1)                     ; wait
         bpl .-2
         mov #10,(r1)                  ; issue cmd - unit + rhdr
         tstb (r1)                     ; wait
         bpl .-2
         mov 6(r1),r2                  ; get hdr
         bic #77,r2                    ; clear sector
         inc r2                        ; magic bit
         mov r2,4(r1)                  ; seek to 0
         mov #6,(r1)                   ; issue cmd - unit + seek
         tstb (r1)                     ; wait
         bpl .-2
         clr 2(r1)                     ; clr bar
         clr 4(r1)                     ; clr dar
         mov #-512.,6(r1)              ; set wc to read 512 words
         mov #14,(r1)                  ; issue cmd - unit + read
         tstb (r1)                     ; wait
         bpl .-2

         bic #377,(r1)
         clr r2
         clr r3
         mov #2000+20,r4
         clr r5
         clr pc
;
nob:
         mov #noboot+173000,r2
         mov #nob2+173000,r5
         jmp barf
nob2:
         halt
;
; subroutines
;
cbarf:
         mov #2000,sp
         clr @#6
         mov r4,@#4
         tst (r1)
         jmp barf
pbarf:
         mov #2000,sp
         clr @#6
         mov r5,@#4
         tst (r1)
barf:
         clr @#4
         clr @#10
         tstb @#177564
         bpl .-4
         movb (r2)+,r3
         beq .+10
         movb r3, @#177566
         br .-16
         jmp (r5)
;
; data
;
         .even

hello:   .ascii / /<15><12>
         .asciz /Hello, world [t38]: cpu 11/<57>
hello2:  .asciz <15><12>

fpu:     .asciz / fpu/
kt11a:   .asciz / mmu/
ubma:    .asciz / ubm/
kw11a:   .asciz / lck/
slu:     .asciz / tt/
;rk11a:   .asciz / rk/
;rl11a:   .asciz / rl/

bootrk:  .asciz /boot from rk:/<15><12>
bootrl:  .asciz /boot from rl:/<15><12>
noboot:  .asciz /no dev/<15><12>
         .even
